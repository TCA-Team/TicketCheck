plugins {
    id 'io.gitlab.arturbosch.detekt' version "1.0.0.RC6-4"
    id 'de.aaschmid.cpd' version '1.0'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../gradle/scripts/findbugs.gradle'
apply from: '../gradle/scripts/pmd.gradle'
apply from: '../gradle/scripts/jacoco.gradle'
apply from: '../gradle/scripts/checkstyle.gradle'
apply from: '../gradle/scripts/detekt.gradle'
apply from: '../gradle/scripts/testLogging.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '27.0.3'
    buildTypes {
        all {
            proguardFiles(file('../proguard').listFiles())
            proguardFile getDefaultProguardFile('proguard-android.txt')
            minifyEnabled true
            useProguard true
            shrinkResources true
        }
        release.debuggable false
        debug {
            debuggable true
            testCoverageEnabled true
        }
    }
    defaultConfig {
        applicationId "de.tum.in.tca.ticketcheck"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        kapt.arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    lintOptions {
        checkAllWarnings true
        htmlReport false
        abortOnError false
        lintConfig new File("gradle/config/lint.xml")
        disable 'GoogleAppIndexingWarning',
                'TrulyRandom',
                'ContentDescription',
                'UnusedIds',
                'UnusedResources',    // unused resources get purged by shrinkResources
                'SelectableText'      // disabled by default
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def androidSupportVersion = '28.0.0'
def retrofitVersion = '2.5.0'
def roomVersion = '1.1.1'
def ankoVersion = '0.10.8'

configurations.all {
    resolutionStrategy {
        force "com.android.support:exifinterface:$androidSupportVersion"
        force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

dependencies {

    // Support libraries
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    releaseImplementation 'com.google.firebase:firebase-core:16.0.5'

    // Android architecture components
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    kapt 'androidx.room:room-compiler:2.1.0-alpha02'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$ankoVersion"

    // Helpers
    implementation 'net.danlew:android.joda:2.10.1.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // Transport
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // QR code scanning
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
}

apply plugin: 'com.google.gms.google-services'
